apply plugin: 'android'


loadProperties("signing.properties")

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 2
        versionName "1.0.1"
    }

    signingConfigs {
        release {
            storeFile file("mcxiaoke.keystore")
            storePassword project.ext.get("storePassword")
            keyAlias project.ext.get("keyAlias")
            keyPassword project.ext.get("keyPassword")
        }
    }

    buildTypes {

        release {
            runProguard false
            signingConfig signingConfigs.release
        }

        beta {
            signingConfig signingConfigs.release
            versionNameSuffix "-beta"
            debuggable true
        }

        debug {
            applicationIdSuffix null
            versionNameSuffix "-dev"
        }
    }

    lintOptions {
        quiet true
        abortOnError false
    }
}

dependencies {

    compile 'com.jakewharton:butterknife:5.1.1'

    compile 'com.mcxiaoke.volley:library:1.+@aar'
    compile 'com.mcxiaoke.next:core:1.+@aar'
    compile 'com.mcxiaoke.next:http:1.+@aar'
    compile 'com.mcxiaoke.next:ui:1.+@aar'


}


def loadProperties(String fileName) {
    def config = new Properties()
    def propFile = file(fileName)
    System.err.println("load property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            System.out.println("property " + property.key + ":" + property.value)
            project.ext[property.key] = property.value;
        }
    }
}

